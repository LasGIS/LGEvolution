include include/import.rout;

/**
 * Стратегия умного перехода.
 */
routine smartFindFood() cellBest, название_еды {
    /* owner.cell */
    /* owner.stomach */

    cellBest = owner.cell;
    foodIndex = owner.stomach.bestFoodIndex(cellBest, 0.0);
    // находим самое вкусное место
    for (cellInd : owner.cell.near(20)) {
        distance = owner.cell.distance(cellInd);
        foodIndexT = owner.stomach.bestFoodIndex(cellInd, distance);

        if (foodIndex.bestIndex < foodIndexT.bestIndex
            && cellInd.element("Камень").value < 5.0) {
            foodIndex = foodIndexT;
            cellBest = cellInd;
        }
    }
    название_еды = foodIndex.bestKey;
}

/**
 * Стратегия умного перехода.
 */
routine smartRunTo(endCell, foodName) {
    runCellStack = new CellStack(9);
    do {
        smartFindWay(endCell) nextPoint, isCellBusy;
        if (!isCellBusy) {
            runCellStack.add(nextPoint);
            nextPoint.element("Дорога").incValue(0.1);
            RunTo(nextPoint);
            //Log("info", "nextPoint = " + nextPoint);
        }
    } while (endCell != nextPoint && !isCellBusy);
}

/**
 * Находим ближайший проход.
 */
routine smartFindWay(endCell) nearCell, setCellBusy {
    nearCell = owner.cell;
    minDist = 1000000000.0;
    setCellBusy = (
        endCell.element("Камень").value >= 5.0
        || endCell.animals.size() > 2
    );

    if (!setCellBusy) {
        // находим ближайшее место
        for (cellInd : owner.cell.near(2)) {
            distance = cellInd.distance(endCell) + owner.cell.distance(cellInd) / cellInd.moveTrouble();
            //Log("cellInd: " + cellInd + "; distance: " + distance + "; trouble: " + cellInd.moveTrouble() + ";");
            if (minDist > distance
                && runCellStack.notContain(cellInd)
                && cellInd.element("Камень").value < 5.0
            ) {
                minDist = distance;
                nearCell = cellInd;
            }
        }
    }
}